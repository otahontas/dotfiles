# Create big enough history file, disable duplicates and allow different zsh
# shells to share history all the time (e.g. don't wait until shell is closed)
[[ -e $XDG_DATA_HOME/zsh ]] || mkdir $XDG_DATA_HOME/zsh
HISTFILE=$XDG_DATA_HOME/zsh/histfile
HISTSIZE=10000
SAVEHIST=$HISTSIZE
setopt sharehistory
setopt incappendhistory
setopt histexpiredupsfirst
setopt histignoredups
setopt histfindnodups
setopt histreduceblanks

# Allow cd'ing without writing cd
setopt autocd
setopt extendedglob

# Set vi-mode to launch immediately
export KEYTIMEOUT=1

# Load brew
source $ZDOTDIR/integrations/brew.zsh

# Add otherwise untrusted brew site-function to fpath
fpath+=$(brew --prefix)/share/zsh/site-functions

# Set up completions, avoid executing compinit on every launch
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion::complete:*' gain-privileges 1
zstyle :compinstall filename '$ZDOTDIR/.zshrc'
autoload -Uz compinit
() {
  if [[ $# -gt 0 ]]; then
    compinit
    autoload bashcompinit && bashcompinit
  else
    compinit -C
  fi
} ${ZDOTDIR}/.zcompdump(N.mh+24)
setopt correct
setopt correctall
setopt no_list_ambiguous
setopt globdots

# Disable command to replay last command
disable r

# Load plugins (from https://getantidote.github.io/install)
# Set the root name of the plugins files (.txt and .zsh) antidote will use.
zsh_plugins=${ZDOTDIR:-~}/.zsh_plugins
# Ensure the .zsh_plugins.txt file exists so you can add plugins.
[[ -f ${zsh_plugins}.txt ]] || touch ${zsh_plugins}.txt
# Lazy-load antidote from its functions directory.
fpath=($(brew --prefix)/opt/antidote/share/antidote/functions $fpath)
autoload -Uz antidote
# Generate a new static file whenever .zsh_plugins.txt is updated.
if [[ ! ${zsh_plugins}.zsh -nt ${zsh_plugins}.txt ]]; then
  antidote bundle <${zsh_plugins}.txt >|${zsh_plugins}.zsh
fi
# Source static plugins file.
source ${zsh_plugins}.zsh

# Set up binding for history search
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

# Source aliases and custom functions after everything is loaded
source $ZDOTDIR/.zsh_aliases
source $ZDOTDIR/.zsh_functions

# Load settings for various programs
for f in $ZDOTDIR/integrations/*.zsh; do
  source "$f"
done

# Add custom local bin to PATH
[[ $PATH != *"$HOME/.local/bin"* ]] && export PATH=$HOME/.local/bin:$PATH

# Load starship prompt
eval "$(starship init zsh)"

# Load custom local zsh config if present
autoload -U add-zsh-hook
load-local-conf() {
  local filename=".local_zshrc"
  if [[ -f $filename && -r $filename ]]; then
    echo "Loading local .zshrc"
    source $filename
  fi
}
load-local-conf
add-zsh-hook chpwd load-local-conf
