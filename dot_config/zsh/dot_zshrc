# Set correct tty for GPG, load agent
export GPG_TTY=$(tty)
gpg-connect-agent updatestartuptty /bye >/dev/null

# Set SSH to use gpg-agent instead of ssh-agent
if [ "${gnupg_SSH_AUTH_SOCK_by:-0}" -ne $$ ]; then
    unset SSH_AGENT_PID
    export SSH_AUTH_SOCK="$(gpgconf --list-dirs agent-ssh-socket)"
fi

# Fnm might print a warning, so initialize it before p10k
source $ZDOTDIR/integrations/fnm.zsh

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.config/zsh/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# To customize prompt, run `p10k configure` or edit ~/.config/zsh/.p10k.zsh.
[[ ! -f ~/.config/zsh/.p10k.zsh ]] || source ~/.config/zsh/.p10k.zsh

# Create big enough history file, disable duplicates and allow different zsh
# shells to share history all the time (e.g. don't wait until shell is closed)
[[ -e $XDG_DATA_HOME/zsh ]] || mkdir $XDG_DATA_HOME/zsh
HISTFILE=$XDG_DATA_HOME/zsh/histfile
HISTSIZE=5000
SAVEHIST=$HISTSIZE
setopt sharehistory
setopt incappendhistory
setopt histexpiredupsfirst
setopt histignoredups
setopt histfindnodups
setopt histreduceblanks

# Allow cd'ing without writing cd
setopt autocd
setopt extendedglob

# Set vi-mode to launch immediately
export KEYTIMEOUT=1

# Add otherwise untrusted brew site-function to fpath
fpath+=$(brew --prefix)/share/zsh/site-functions

# Set up completions, avoid executing compinit on every launch
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion::complete:*' gain-privileges 1
zstyle :compinstall filename '$ZDOTDIR/.zshrc'
autoload -Uz compinit
() {
  if [[ $# -gt 0 ]]; then
    compinit
    autoload bashcompinit && bashcompinit
  else
    compinit -C
  fi
} ${ZDOTDIR:-$HOME}/.zcompdump(N.mh+24)
setopt correct
setopt correctall
setopt no_list_ambiguous
setopt complete_aliases
setopt globdots

# Disable command to replay last command
disable r

# Load plugins
# To update plugins, run antibody bundle < .zsh_plugins.txt > .zsh_plugins.sh
source $ZDOTDIR/.zsh_plugins.sh

# Set up binding for history search
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

# Source aliases and custom functions after everything is loaded
source $ZDOTDIR/.zsh_aliases
source $ZDOTDIR/.zsh_functions

# Load settings for various programs
source $ZDOTDIR/integrations/bat.zsh
source $ZDOTDIR/integrations/brew.zsh
source $ZDOTDIR/integrations/fzf.zsh
source $ZDOTDIR/integrations/go.zsh
source $ZDOTDIR/integrations/google-cloud-sdk.zsh
source $ZDOTDIR/integrations/nnn.zsh
source $ZDOTDIR/integrations/nvim.zsh
source $ZDOTDIR/integrations/pyenv.zsh
source $ZDOTDIR/integrations/ssh.zsh

# Add custom local bin to PATH
[[ $PATH != *"$HOME/.local/bin"* ]] && export PATH=$HOME/.local/bin:$PATH
